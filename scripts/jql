#!/usr/bin/env perl
use strict; use warnings;#{{{
use FindBin;
use lib "$FindBin::RealBin/../lib/perl5", "$FindBin::RealBin/../lib";
use List::MoreUtils qw/any/;
use 5.010;
use Getopt::Long qw/:config bundling/;
use Ticket;
#}}}

# FIXME hack to jumpstart cfg in this package context
Ticket::cfg('format');

my %O = (
    field => [qw/key summary/],
);
my $rtfm = not GetOptions(
    'h|help+'       => \$O{help},
    'f|field=s@'    => \$O{field},
    'l|link|url'    => \$O{url},
    'v|verbose'     => sub { Ticket::session_cfg(verbose => 1) },
    'format=s'      => \$O{format},
    'c|count'       => \$O{count},
    'g|groupby=s'   => \$O{groupby},
);
if ($O{groupby}) {
    warn "Overriding field opt with groupby" if @{ $O{field} };
    $O{field} = ['key', $O{groupby}];
}
if (!$O{help} && ($rtfm || ! @ARGV)) {
    $O{help} = 1;
}
if ($O{help}) {
    require Pod::Usage;
    Pod::Usage::pod2usage({-verbose => $O{help}-1, -exitval => $rtfm});
}
if ($O{format}) {
    Ticket::session_cfg('format', $O{format});
}

my $tracker = Ticket::tracker;
my @tickets = $tracker->search(query => (join ' ', @ARGV), fields => $O{field});

if ($O{count}) {
    say scalar @tickets;
    exit;
}
if ($O{groupby}) {
    my $field = $O{groupby};
    my %count;
    for (@tickets) {
        $count{$_->{$field}}++;
    }
    for (sort { $count{$b} <=> $count{$a} } keys %count) {
        say "$count{$_}\t$_";
    }
    exit;
}

if ($O{url}) {
    $_->{key} = $tracker->issue_url($_->{key}) for @tickets;
}

Ticket::ticket_out(\@tickets, @{$O{field}});
exit ! @tickets;
__END__
=pod

=head1 NAME

jql - shell interface to Jira issue search

=head1 SYNOPSIS

jql [-f FIELD...] [-l] JQL_QUERY [--format FORMAT] [-c]

=head1 DESCRIPTION

Searches for issues in Jira with JQL_QUERY.
Results are displayed by key and summary, one per line.

When connecting to Jira, uses credentials stored in ~/.jirarc

=head1 OPTIONS

-h, --help
   Display this text. Ignores other options.

-f, --field
   Specify what fields of tickets found to show.
   Fields displayed in order given.

-l, --link, --url
   Display url to issue instead of issue key.

--format FORMAT
   Specify different FORMAT, currently JSON is the only supported alternative.

-c, --count
   Only return the number of matching issues.

=head1 EXAMPLES

jql -f summary -f assignee component="Team U" and status="In QA"

jql assignee=$USER and status=open
    List of user's own assignments, by key and summary

jql -f key -f summary -l fixversion=v4.101.4
    List of tasks to be tested on staging, with links for convenience

=head1 AUTHOR

bieron@github, feedback welcomed

=cut
